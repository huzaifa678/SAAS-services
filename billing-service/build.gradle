plugins {
    id 'java'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.springframework.boot' version '4.0.2'
    id 'com.google.protobuf' version '0.9.5'
    id 'org.asciidoctor.jvm.convert' version '4.0.5'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
}


group = 'com.project'
version = '0.0.1-SNAPSHOT'
description = 'billing service'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://packages.confluent.io/maven/"
    }
}

ext {
    set('snippetsDir', file("build/generated-snippets"))
    set('springGrpcVersion', "1.0.2")
    set('springCloudVersion', "2025.1.0")
    set('confluentVersion', "7.6.1")
    set('avroVersion', "1.11.3")
    set('springBootVersion', "4.0.2")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation "com.bucket4j:bucket4j_jdk17-core:8.15.0"
    implementation "com.bucket4j:bucket4j_jdk17-lettuce:8.15.0"
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.stripe:stripe-java:31.3.0'
    implementation 'org.springframework.boot:spring-boot-starter-kafka'
    implementation "org.apache.avro:avro:${avroVersion}"
    implementation "io.confluent:kafka-avro-serializer:${confluentVersion}"
    implementation 'org.springframework.grpc:spring-grpc-server-web-spring-boot-starter'
    implementation 'io.grpc:grpc-netty-shaded'
    implementation 'io.grpc:grpc-stub'
    implementation 'io.grpc:grpc-protobuf'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'

    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    implementation 'io.micrometer:micrometer-tracing-bridge-otel'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp'
    implementation 'org.springframework.boot:spring-boot-starter'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jdbc-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.boot:spring-boot-starter-kafka-test'
    testImplementation 'org.springframework.grpc:spring-grpc-test'
    testImplementation 'org.testcontainers:testcontainers-junit-jupiter'
    testImplementation 'org.testcontainers:testcontainers-kafka'
    testImplementation 'org.testcontainers:testcontainers-postgresql'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.springframework.grpc:spring-grpc-dependencies:${springGrpcVersion}"
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.58.0"
        }
    }
    generateProtoTasks {
        all().each { task ->

            task.builtins {
                java {}
            }

            task.plugins {
                grpc {}
            }
        }
    }
}

avro {
    stringType = "String"
    fieldVisibility = "PRIVATE"
    createSetters = false
}

sourceSets {
    main {
        java {
            srcDirs += "$buildDir/generated/sources/proto/main/java"
            srcDirs += "$buildDir/generated/sources/proto/main/grpc"
            srcDirs += "$buildDir/generated-main-avro-java"
        }
    }
}

tasks.named("compileJava") {
    dependsOn(tasks.withType(com.google.protobuf.gradle.GenerateProtoTask))
}

tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}
